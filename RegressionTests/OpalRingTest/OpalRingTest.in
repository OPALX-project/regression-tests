Option, ECHO=TRUE;
//////////////////////////////////////////////////////////////////////////////
// Input file for single bunch tracking through ERIT FFAG ring              //
//////////////////////////////////////////////////////////////////////////////
Title,string="ERIT test simulation using OPAL code";
Option, ASCIIDUMP=TRUE;
OPTION, PSDUMPFREQ=100000; 
Option, PSDUMPLOCALFRAME=TRUE;
OPTION, VERSION=10600;

REAL Edes=0.011;
REAL e_tot = (Edes+PMASS);
REAL p_tot = (e_tot^2-PMASS^2)^0.5;
REAL gamma = e_tot/PMASS;
REAL beta = p_tot/e_tot;
REAL gambet = gamma*beta;
REAL P0 = gamma*beta*PMASS;
REAL brho = (PMASS*1.0e9*gambet) / CLIGHT;
REAL x_closed_orbit = 2349.008;

// print to terminal
value,{gamma,brho,Edes,beta,gambet,x_closed_orbit};

REAL n_turns=100.0;
REAL revolution_period=327;

REAL step_size=0.1; // ns
REAL max_steps=n_turns*revolution_period/step_size;
REAL steps_per_turn=revolution_period/step_size;
REAL frequency=6000/revolution_period;

value,{step_size,n_turns,revolution_period,step_size};

Probe1: PROBE, xstart=0, xend=+4000., ystart=0, yend=0.;

ringdef: RINGDEFINITION, HARMONIC_NUMBER=6, LAT_RINIT=2350.0, LAT_PHIINIT=0.0,
         LAT_THETAINIT=0.0, BEAM_PHIINIT=0.0, BEAM_PRINIT=0.0,
         BEAM_RINIT=x_closed_orbit, SYMMETRY=8.0, RFFREQ=frequency, IS_CLOSED=true;

triplet: SBEND3D, FMAPFN="OpalRingTest.field", LENGTH_UNITS=10., FIELD_UNITS=1e-3;

l1: Line = (ringdef, probe1, triplet);

Dist1: DISTRIBUTION, DISTRIBUTION=fromfile, FNAME="beam.dat", INPUTMOUNITS=NONE;

Fs1:FIELDSOLVER, FSTYPE=NONE, MX=2, MY=2, MT=2, 
		 PARFFTX=true, PARFFTY=true, PARFFTT=false,
		 BCFFTX=open, BCFFTY=open, BCFFTT=open,BBOXINCR=2;

beam1: BEAM, PARTICLE=PROTON, pc=P0, NPART=12, BCURRENT=1.0E-19, CHARGE=1.0, BFREQ=frequency;

TRACK, LINE=l1, BEAM=beam1, MAXSTEPS=max_steps, STEPSPERTURN=steps_per_turn;
RUN, METHOD="CYCLOTRON-T", BEAM=beam1, FIELDSOLVER=Fs1, DISTRIBUTION=Dist1;
ENDTRACK;
STOP;
