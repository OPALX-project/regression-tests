OPTION, ECHO=FALSE;
OPTION, PSDUMPFREQ=4500;
OPTION, PSDUMPFRAME=BUNCH_MEAN;
OPTION, SPTDUMPFREQ=1000000;
OPTION, ENABLEHDF5=TRUE;
OPTION, PSDUMPEACHTURN=TRUE;
OPTION, REPARTFREQ=5;
OPTION, STATDUMPFREQ=1;
OPTION, CZERO=FALSE;
OPTION, VERSION=20000;
OPTION, SCSOLVEFREQ=2;
OPTION, AMR=TRUE;
OPTION, AMR_REGRID_FREQ=10;
OPTION, AMR_YT_DUMP_FREQ=1000000;
OPTION, MEMORYDUMP=TRUE;
OPTION, TELL=TRUE;

TITLE, string="OPAL-cycl: Neighboring bunch AMR test in PSI 590MeV Ring where we track two bunches by three turns. It uses the AMR_MG Poisson solver based on Trilinos packages and two AMR levels.";

REAL Edes=.072;
REAL gamma=(Edes+PMASS)/PMASS;
REAL beta=sqrt(1-(1/gamma^2));
REAL gambet=gamma*beta;
REAL P0 = gamma*beta*PMASS;
REAL brho = (PMASS*1.0e9*gambet) / CLIGHT;

//value,{gamma,brho,Edes,beta,gambet};

REAL phi01=139.4281;
REAL phi02=phi01+180.0;
REAL phi04=phi01;
REAL phi05=phi01+180.0;
REAL phi03=phi01+10.0;

REAL volt1st=0.9;
REAL volt3rd=0.9*4.0*0.112;

REAL turns=3;
REAL nstep=360;

REAL frequency=50.650;
REAL frequency3=3.0*frequency;

ringCyclotron: CYCLOTRON, TYPE=RING, CYHARMON=6, PHIINIT=0.0,
        PRINIT=-0.000174, RINIT=2130.0, SYMMETRY=8.0, RFFREQ=frequency,
        FMAPFN="s03av.nar";

rf0: RFCAVITY, VOLT=volt1st, FMAPFN="Cav1.dat", TYPE=SINGLEGAP,
        FREQ=frequency, RMIN = 1900.0, RMAX = 4500.0, ANGLE=35.0,  PDIS = 416.0,
        GAPWIDTH = 220.0, PHI0=phi01;

rf1: RFCAVITY, VOLT=volt1st, FMAPFN="Cav1.dat", TYPE=SINGLEGAP,
        FREQ=frequency, RMIN = 1900.0, RMAX = 4500.0, ANGLE=125.0, PDIS = 416.0,
        GAPWIDTH = 220.0, PHI0=phi02;

rf2: RFCAVITY, VOLT=volt3rd, FMAPFN="Cav3.dat", TYPE=SINGLEGAP,
        FREQ=frequency3,RMIN = 1900.0, RMAX = 4500.0, ANGLE=170.0, PDIS = 452.0,
        GAPWIDTH = 250.0, PHI0=phi03;

rf3: RFCAVITY, VOLT=volt1st, FMAPFN="Cav1.dat", TYPE=SINGLEGAP,
        FREQ=frequency, RMIN = 1900.0, RMAX = 4500.0, ANGLE=215.0, PDIS = 416.0,
        GAPWIDTH = 220.0, PHI0=phi04;

rf4: RFCAVITY, VOLT=volt1st, FMAPFN="Cav1.dat", TYPE=SINGLEGAP,
        FREQ=frequency, RMIN = 1900.0, RMAX = 4500.0, ANGLE=305.0, PDIS = 416.0,
        GAPWIDTH = 220.0, PHI0=phi05;

line1: LINE = (ringCyclotron,rf0,rf1,rf2,rf3,rf4);

Dist1: DISTRIBUTION, TYPE=GAUSS,
        SIGMAX = 2.0e-03,
        SIGMAPX = 1.0e-7,
        CORRX = 0.0,
        SIGMAY = 2.0e-03,
        SIGMAPY = 1.0e-7,
        CORRY = 0.0,
        SIGMAT = 2.0e-03,
        SIGMAPT = 3.394e-4,
        CORRT=0.0;

Fs1: FIELDSOLVER, FSTYPE=AMR_MG, MX=32, MY=32, MT=32,
        PARFFTX=TRUE, PARFFTY=TRUE, PARFFTT=TRUE,
        BCFFTX=DIRICHLET, BCFFTY=DIRICHLET, BCFFTT=DIRICHLET,
        BBOXINCR=20, AMR_MAXLEVEL=2, AMR_MAXGRIDX=32, AMR_MAXGRIDY=32,
        AMR_MAXGRIDZ=32, AMR_BFX=16, AMR_BFY=16, AMR_BFZ=16,
        AMR_REFX=2, AMR_REFY=2, AMR_REFZ=2, AMR_DOMAIN_RATIO={1.0, 0.75, 0.75},
        AMR_TAGGING="CHARGE_DENSITY", AMR_DENSITY=1.0e-9,
        AMR_MG_REBALANCE=TRUE, AMR_MG_NSWEEPS=12, ITSOLVER=SA, AMR_MG_NORM=LINF_NORM;

beam1: BEAM, PARTICLE=PROTON, pc=P0, NPART=32*32*32, BCURRENT=2.0E-3, BFREQ=frequency;

SELECT, LINE=line1;

TRACK, LINE=line1, BEAM=beam1, MAXSTEPS=nstep*turns, STEPSPERTURN=nstep, TIMEINTEGRATOR="RK-4";

RUN, METHOD="CYCLOTRON-T", BEAM=beam1, FIELDSOLVER=Fs1, DISTRIBUTION=Dist1,
        MBMODE=FORCE, TURNS=2, MB_BINNING=GAMMA_BINNING, MB_ETA=0.25;

ENDTRACK;
STOP;
