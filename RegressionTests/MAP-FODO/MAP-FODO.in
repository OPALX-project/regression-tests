OPTION, ECHO=FALSE;
OPTION, INFO=TRUE;
Option, PSDUMPFREQ=1;
Option, SPTDUMPFREQ = 50;
Option, STATDUMPFREQ=1;
OPTION, VERSION=10900;

TITLE, STRING="FODO Test";

REAL Edes   = 0.10;
REAL gamma  = (Edes+PMASS)/PMASS;
REAL beta   = sqrt(1-(1/gamma^2));
REAL gambet = gamma*beta;
REAL P0     = gamma*beta*PMASS;
REAL brho   = (PMASS*1.0e9*gambet) / CLIGHT;
REAL rf     = 50.6328e6;  //need to be confirmed

VALUE,{gamma,brho,Edes,beta,gambet};

D1:  DRIFT,             L=1.,                ELEMEDGE=0.000, NSLICES=10;
QP1: QUADRUPOLE,        L=0.3,  K1= 8.64195, ELEMEDGE=1.000, NSLICES=15;
DG1: DRIFT,             L=0.3,               ELEMEDGE=1.300, NSLICES=10;
QP2: QUADRUPOLE,        L=0.3,  K1=-11.57456,ELEMEDGE=1.600, NSLICES=15;
DG2: DRIFT,             L=0.3,               ELEMEDGE=1.900, NSLICES=10;
QP3: QUADRUPOLE,        L=0.3,  K1= 8.64195, ELEMEDGE=2.200, NSLICES=15;
D3:  DRIFT,             L=.9,                ELEMEDGE=2.500, NSLICES=5;
D4:  DRIFT,             L=.3,                ELEMEDGE=3.400, NSLICES=20;
QUADTEST: LINE=( D1, QP1 , DG1, QP2, DG2, QP3, D3, D4);

DIST1: DISTRIBUTION, TYPE=GAUSS,
SIGMAX  =  0,     SIGMAPX = 1.0e06,  CORRX = 0.0,
SIGMAY  =  0,     SIGMAPY = 1.0e06,  CORRY = 0.0,
OFFSETZ =  0.005, SIGMAT  = 0.0005,
SIGMAPT= 0.0, CORRT= 0.0, R61= 0.0,
INPUTMOUNITS=EVOVERC, WRITETOFILE= TRUE;

DIST: DISTRIBUTION, TYPE = FROMFILE, FNAME="data/FODO_DIST.dat"; //FNAME="PartDatabase.dat";

FS1:FIELDSOLVER, FSTYPE=NONE, MX=64, MY=64, MT=64,
                 PARFFTX=true, PARFFTY=true, PARFFTT=true,
                 BCFFTX=open, BCFFTY=open, BCFFTT=open,
                 BBOXINCR=1, GREENSF=STANDARD;

BEAM1: BEAM, PARTICLE=PROTON, PC=P0, NPART=200, BCURRENT=2.0e-03, BFREQ=rf, CHARGE=1;

SELECT, LINE=QUADTEST;


TRACK, LINE= QUADTEST, BEAM=BEAM1, MAXSTEPS=10000, DT=1.0e-10, ZSTOP=4.0, MAP_ORDER=2;

RUN, METHOD = "THICK", BEAM=BEAM1, FIELDSOLVER=FS1, DISTRIBUTION=DIST;

ENDTRACK;

STOP;
