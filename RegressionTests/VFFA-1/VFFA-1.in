Option, ECHO=TRUE;
//////////////////////////////////////////////////////////////////////////
// Input file for single particle tracking through VFFA ring            //
// The input particle should be on a closed orbit; so should return to  //
// the same point as injected in all the loss files                     //
//////////////////////////////////////////////////////////////////////////
Title,string="Small ring using OPAL code";
Option, ASCIIDUMP=TRUE;
Option, ENABLEHDF5=FALSE;
OPTION, PSDUMPFREQ=100000;
Option, VERSION=10900;
Option, SPTDUMPFREQ=100;
Option, STATDUMPFREQ=100000;

////////// CONSTANTS ////////////////////////////////////

REAL DEGREE=PI/180.;
REAL MM=1000.;
REAL C_LIGHT=0.3; // m/ns

////////// RING PARAMETERS ///////////////
REAL R0=14.350158350258441;
REAL E0=400.0;
REAL P_MASS=938.2720813;
REAL P0=((E0+P_MASS)^2-P_MASS^2)^0.5;
REAL N_CELLS=15;
REAL RMIN=R0-1.0;
REAL RMAX=R0+1.0;
REAL LAT_PHI_INIT=0.0;

////////// TRACKING ///////////////
REAL STEP_SIZE=0.01; // m
REAL N_TURNS=2.01;
REAL N_PARTICLES=1;

////////// MAIN MAGNET PARAMETERS/////////
REAL M_INDEX=0.87721;
REAL F_CENTRE_LENGTH=1.2;
REAL F_END_LENGTH=0.3;
REAL D_CENTRE_LENGTH=1.2;
REAL D_END_LENGTH=0.3;
REAL BF=-4.92;
REAL BD=1.64;
REAL MAX_HORIZONTAL_POWER=10;
REAL NEG_EXTENT=1.0;
REAL POS_EXTENT=3.0;
REAL BB_LENGTH=8.0; 
REAL MAG_WIDTH=10.0;

REAL D_OFFSET=-0.347745;
REAL F_OFFSET=0.347745;

REAL DRIFT_1=1.8;
REAL DRIFT_2=1.8;

//////////// MAIN MAGNETS /////////////////////
magnetF: VERTICALFFAMAGNET,
          B0=BF,
          FIELD_INDEX=M_INDEX,
          MAX_HORIZONTAL_POWER=MAX_HORIZONTAL_POWER,
          END_LENGTH=F_END_LENGTH,
          CENTRE_LENGTH=F_CENTRE_LENGTH/2,
          HEIGHT_NEG_EXTENT=NEG_EXTENT,
          HEIGHT_POS_EXTENT=POS_EXTENT,
          WIDTH=MAG_WIDTH,
          BB_LENGTH=BB_LENGTH;

magnetD: VERTICALFFAMAGNET,
          B0=BD,
          FIELD_INDEX=M_INDEX,
          MAX_HORIZONTAL_POWER=MAX_HORIZONTAL_POWER,
          END_LENGTH=D_END_LENGTH,
          CENTRE_LENGTH=D_CENTRE_LENGTH/2,
          HEIGHT_NEG_EXTENT=NEG_EXTENT,
          HEIGHT_POS_EXTENT=POS_EXTENT,
          WIDTH=MAG_WIDTH,
          BB_LENGTH=BB_LENGTH;

///////////////////////// PLACEMENTS FOR MAGNET VD1 //////////////////////

vd_offset_out: LOCAL_CARTESIAN_OFFSET,
                end_position_x=D_OFFSET,
                end_position_y=D_CENTRE_LENGTH/2.+DRIFT_1/2.-BB_LENGTH/2,
                end_normal_x=0.0,
                end_normal_y=1.0;

vd_offset_back: LOCAL_CARTESIAN_OFFSET,
                end_position_x=-D_OFFSET,
                end_position_y=-D_CENTRE_LENGTH/2.-DRIFT_1/2.-BB_LENGTH/2,
                end_normal_x=0.0,
                end_normal_y=1.0;

///////////////////////////// D_SINGLE ///////////////////////////////

d_single: Line = (vd_offset_out, magnetD, vd_offset_back);

//////////////////////////// DRIFT /////////////////////////////////

REAL D_HALF_CELL_LENGTH=D_CENTRE_LENGTH+DRIFT_1/2.+DRIFT_2/2.;
REAL D_HALF_CELL_ANGLE=360/N_CELLS/2*DEGREE;

start_offset: LOCAL_CARTESIAN_OFFSET,
            end_position_x=0.0,
            end_position_y=-D_HALF_CELL_LENGTH,
            end_normal_y=0.0,
            end_normal_y=cos(D_HALF_CELL_ANGLE);

d_half_cell_drift: LOCAL_CARTESIAN_OFFSET,
                end_position_x=0,
                end_position_y=D_HALF_CELL_LENGTH,
                end_normal_x=-sin(D_HALF_CELL_ANGLE),
                end_normal_y=cos(D_HALF_CELL_ANGLE);

///////////////////////// PLACEMENTS FOR MAGNET VF1 //////////////////////

vf_offset_out: LOCAL_CARTESIAN_OFFSET,
                end_position_x=F_OFFSET,
                end_position_y=F_CENTRE_LENGTH/2.+DRIFT_2/2.-BB_LENGTH/2,
                end_normal_x=0.0,
                end_normal_y=1.0;

vf_offset_back: LOCAL_CARTESIAN_OFFSET,
                end_position_x=-F_OFFSET,
                end_position_y=-F_CENTRE_LENGTH/2.-DRIFT_2/2.-BB_LENGTH/2,
                end_normal_x=0.0,
                end_normal_y=1.0;


///////////////////////////// F_SINGLE ///////////////////////////////

f_single: Line = (vf_offset_out, magnetF, vf_offset_back);

//////////////////////////// DRIFT /////////////////////////////////

REAL F_HALF_CELL_LENGTH=F_CENTRE_LENGTH+DRIFT_1/2.+DRIFT_2/2.;
REAL F_HALF_CELL_ANGLE=360/N_CELLS/2*DEGREE;

f_half_cell_drift: LOCAL_CARTESIAN_OFFSET,
                end_position_x=0,
                end_position_y=F_HALF_CELL_LENGTH,
                end_normal_x=-sin(F_HALF_CELL_ANGLE),
                end_normal_y=cos(F_HALF_CELL_ANGLE);

////////////////// PROBES
REAL RING_PROBE_PHI_OFFSET=0.0;
BUILD_PROBE(NAME, ANGLE): MACRO {
    NAME: PROBE, xstart=RMIN*1000*cos(ANGLE),  xend=RMAX*1000*cos(ANGLE),
                 ystart=RMIN*1000*sin(ANGLE),  yend=RMAX*1000*sin(ANGLE);
}

REAL THIS_PROBE_PHI=0;
BUILD_PROBE(RingProbe01, THIS_PROBE_PHI);
THIS_PROBE_PHI = EVAL(THIS_PROBE_PHI+2.*PI/N_CELLS);
BUILD_PROBE(RingProbe02, THIS_PROBE_PHI);
THIS_PROBE_PHI = EVAL(THIS_PROBE_PHI+2.*PI/N_CELLS);
BUILD_PROBE(RingProbe03, THIS_PROBE_PHI);
THIS_PROBE_PHI = EVAL(THIS_PROBE_PHI+2.*PI/N_CELLS);
BUILD_PROBE(RingProbe04, THIS_PROBE_PHI);
THIS_PROBE_PHI = EVAL(THIS_PROBE_PHI+2.*PI/N_CELLS);
BUILD_PROBE(RingProbe05, THIS_PROBE_PHI);
THIS_PROBE_PHI = EVAL(THIS_PROBE_PHI+2.*PI/N_CELLS);
BUILD_PROBE(RingProbe06, THIS_PROBE_PHI);
THIS_PROBE_PHI = EVAL(THIS_PROBE_PHI+2.*PI/N_CELLS);
BUILD_PROBE(RingProbe07, THIS_PROBE_PHI);
THIS_PROBE_PHI = EVAL(THIS_PROBE_PHI+2.*PI/N_CELLS);
BUILD_PROBE(RingProbe08, THIS_PROBE_PHI);
THIS_PROBE_PHI = EVAL(THIS_PROBE_PHI+2.*PI/N_CELLS);
BUILD_PROBE(RingProbe09, THIS_PROBE_PHI);
THIS_PROBE_PHI = EVAL(THIS_PROBE_PHI+2.*PI/N_CELLS);
BUILD_PROBE(RingProbe10, THIS_PROBE_PHI);
THIS_PROBE_PHI = EVAL(THIS_PROBE_PHI+2.*PI/N_CELLS);
BUILD_PROBE(RingProbe11, THIS_PROBE_PHI);
THIS_PROBE_PHI = EVAL(THIS_PROBE_PHI+2.*PI/N_CELLS);
BUILD_PROBE(RingProbe12, THIS_PROBE_PHI);
THIS_PROBE_PHI = EVAL(THIS_PROBE_PHI+2.*PI/N_CELLS);
BUILD_PROBE(RingProbe13, THIS_PROBE_PHI);
THIS_PROBE_PHI = EVAL(THIS_PROBE_PHI+2.*PI/N_CELLS);
BUILD_PROBE(RingProbe14, THIS_PROBE_PHI);
THIS_PROBE_PHI = EVAL(THIS_PROBE_PHI+2.*PI/N_CELLS);
BUILD_PROBE(RingProbe15, THIS_PROBE_PHI);
THIS_PROBE_PHI = EVAL(THIS_PROBE_PHI+2.*PI/N_CELLS);

ringprobe: Line = (RingProbe01, RingProbe02, RingProbe03, RingProbe04, RingProbe05, 
                   RingProbe06, RingProbe07, RingProbe08, RingProbe09, RingProbe10,
                   RingProbe11, RingProbe12, RingProbe13, RingProbe14, RingProbe15);

ringdef: RINGDEFINITION, HARMONIC_NUMBER=1, LAT_RINIT=R0, LAT_PHIINIT=LAT_PHI_INIT,
         LAT_THETAINIT=360/4/N_CELLS, BEAM_PHIINIT=0, BEAM_PRINIT=0,
         BEAM_RINIT=0.0, SYMMETRY=1, RFFREQ=1, IS_CLOSED=false,
         MIN_R=0., MAX_R=40;
// magnet_d_placement_in, magnet_d_placement_out, 
cell: Line = ( f_half_cell_drift, f_single, d_half_cell_drift, d_single);
line1: Line = (ringdef, ringprobe,
            cell, cell, cell, cell, cell,
            cell, cell, cell, cell, cell,
            cell, cell, cell, cell, cell
           );

Dist1: DISTRIBUTION, TYPE=fromfile, FNAME="disttest.dat", INPUTMOUNITS=NONE;

Fs1:FIELDSOLVER, FSTYPE=None, MX=5, MY=5, MT=5,
                 PARFFTX=true, PARFFTY=false, PARFFTT=false,
                 BCFFTX=open, BCFFTY=open, BCFFTT=open,BBOXINCR=2;

//// STEP SIZE ////

REAL B_FREQ=1; // 1 MHz
REAL SPEED=C_LIGHT*(P0/(E0+P_MASS));
REAL STEP_SIZE_NS=STEP_SIZE/SPEED;
REAL STEPS_PER_TURN=1000/STEP_SIZE_NS/B_FREQ; // (this is really steps per B_FREQ)
REAL MAX_STEPS=2*PI*R0*N_TURNS/STEP_SIZE;

beam1: BEAM, PARTICLE=PROTON, pc=P0/1000., NPART=N_PARTICLES, BCURRENT=1, CHARGE=1.0, BFREQ=B_FREQ;

VALUE, VALUE={E0, P0, P_MASS};

TRACK, LINE=line1, BEAM=beam1, MAXSTEPS=MAX_STEPS, STEPSPERTURN=STEPS_PER_TURN;
RUN, METHOD="CYCLOTRON-T", BEAM=beam1, FIELDSOLVER=Fs1, DISTRIBUTION=Dist1;
ENDTRACK;
STOP;
