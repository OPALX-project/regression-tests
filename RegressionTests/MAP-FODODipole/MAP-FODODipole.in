OPTION, ECHO=FALSE;
OPTION, INFO=TRUE;
Option, PSDUMPFREQ=1;
Option, SPTDUMPFREQ = 50;
Option, STATDUMPFREQ=1;
OPTION, VERSION=10900;

TITLE, STRING="MAP Test";

REAL Edes   = 1.-EMASS;
REAL gamma  = (Edes+EMASS)/EMASS;
REAL beta   = sqrt(1-(1/gamma^2));
REAL gambet = gamma*beta;
REAL P0     = gamma*beta*EMASS;
REAL brho   = -(EMASS*1.0e9*gambet) / CLIGHT;
REAL rf     = 50.6328e6;  //need to be confirmed

VALUE,{gamma,brho,Edes,beta,gambet};




Qf:  QUADRUPOLE,       L=0.2,  K1= -10.940900894055508, ELEMEDGE=0.0, NSLICES=10;
D1:  DRIFT,            L=0.5,                           ELEMEDGE=0.2, NSLICES=5 ;
Sb1: SBEND,            K0= -2.3287040424894165, L=0.9798157487421315, ELEMEDGE=0.7,
        //0.99968272,
        FMAPFN="data/HardEdge.t7",
        DESIGNENERGY=Edes*1e3, GAP=75*2/1000, NSLICES=20;
D2:  DRIFT,            L=0.5,                          ELEMEDGE=1.7, NSLICES=5 ;
Qd:  QUADRUPOLE,       L=0.2,  K1= 11.040970109549919, ELEMEDGE=2.2, NSLICES=10;
D3:  DRIFT,            L=0.5,                          ELEMEDGE=2.4, NSLICES=20 ;
Sb2: SBEND,            K0= -2.3287040424894165, L=0.9798157487421315, ELEMEDGE=2.9,
        FMAPFN="data/HardEdge.t7",
        DESIGNENERGY=Edes*1e3, GAP=75*2/1000, NSLICES=20;
D4:  DRIFT,             L=.5,                          ELEMEDGE=3.9, NSLICES=5 ;
//beamline = [Qf, D, Sb, D, Qd, D, Sb, D]
FODOCELL: LINE=(Qf, D1,Sb1, D2, Qd, D3, Sb2, D4);

REAL emit   = 2*1e-3;

DIST1: DISTRIBUTION, TYPE=GAUSS,
SIGMAX=  7.112474638*emit,   SIGMAPX= 0.9137370817117192*emit,  CORRX= -2.344980132*emit,
SIGMAY=  1.3592143*emit,   SIGMAPY= 0.9320892051315421*emit,  CORRY= 0.5166323417*emit,
OFFSETZ     =  0.005,        SIGMAT= 0.0005,
SIGMAPT= 0.0, CORRT= 0.0, R61= 0.0, INPUTMOUNITS=EV, WRITETOFILE=TRUE;

DIST: DISTRIBUTION, TYPE = FROMFILE, FNAME= "data/FODODipole_DIST.dat";


FS1:FIELDSOLVER, FSTYPE=NONE, MX=64, MY=64, MT=64,
                 PARFFTX=true, PARFFTY=true, PARFFTT=true,
                 BCFFTX=open, BCFFTY=open, BCFFTT=open,
                 BBOXINCR=1, GREENSF=STANDARD;

BEAM1: BEAM, PARTICLE=ELECTRON, PC=P0, NPART=200, BCURRENT=2.0e-03, BFREQ=rf, CHARGE=-1;

SELECT, LINE=FODOCELL;


TRACK, LINE= FODOCELL, BEAM=BEAM1, MAXSTEPS=10000, DT=1.0e-10, ZSTOP=4.4, MAP_ORDER=2;

RUN, METHOD = "THICK", BEAM=BEAM1, FIELDSOLVER=FS1, DISTRIBUTION=DIST;

ENDTRACK;

STOP;
