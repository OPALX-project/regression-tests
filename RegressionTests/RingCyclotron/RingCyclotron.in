Option, ECHO=FALSE;
Option, TFS=FALSE;
Option, PSDUMPLOCALFRAME=FALSE;
Option, STATDUMPFREQ=1;
Option, CZERO=FALSE;
Option, TELL=TRUE;
//////////////////////////////////////////////////////////////////////////////
// input file for single bunch tracking with space charge effects           //
// in parallel inveronment for PSI Ring with 4 NEW copper cavities          //
// the initial conditions are obtained from FIXPO GORBIT mode.              //
//////////////////////////////////////////////////////////////////////////////
Option, PSDUMPFREQ=10000;
Option, SPTDUMPFREQ=10000;
Option, REPARTFREQ=10;
Option, REBINFREQ=50;

Title,string="OPAL-CyclT for PSI 590MeV Ring";

Edes=.072;
gamma=(Edes+PMASS)/PMASS;
beta=sqrt(1-(1/gamma^2));
gambet=gamma*beta;
P0 = gamma*beta*PMASS;
brho = (PMASS*1.0e9*gambet) / CLIGHT;

//value,{gamma,brho,Edes,beta,gambet};

phi01=139.4281;
phi02=phi01+180.0;
phi04=phi01;
phi05=phi01+180.0;
phi03=phi01+10.0;

volt1st=0.9;
volt3rd=0.9*4.0*0.112;

frequency=50.650;
frequency3=3.0*frequency;

ring: Cyclotron, TYPE="RING", CYHARMON=6, PHIINIT=0.0,  
PRINIT=-0.000174, RINIT=2130.0, SYMMETRY=8.0, RFFREQ=frequency, 
FMAPFN="s03av.nar";

rf0: RFCavity, VOLT=volt1st, FMAPFN="Cav1.dat", TYPE="SINGLEGAP", 
FREQ=frequency, RMIN = 1900.0, RMAX = 4500.0, ANGLE=35.0,  PDIS = 416.0, 
GAPWIDTH = 220.0, PHI0=phi01; 

rf1: RFCavity, VOLT=volt1st, FMAPFN="Cav1.dat", TYPE="SINGLEGAP", 
FREQ=frequency, RMIN = 1900.0, RMAX = 4500.0, ANGLE=125.0, PDIS = 416.0, 
GAPWIDTH = 220.0, PHI0=phi02;

rf2: RFCavity, VOLT=volt3rd, FMAPFN="Cav3.dat", TYPE="SINGLEGAP", 
FREQ=frequency3,RMIN = 1900.0, RMAX = 4500.0, ANGLE=170.0, PDIS = 452.0, 
GAPWIDTH = 250.0, PHI0=phi03; 

rf3: RFCavity, VOLT=volt1st, FMAPFN="Cav1.dat", TYPE="SINGLEGAP", 
FREQ=frequency, RMIN = 1900.0, RMAX = 4500.0, ANGLE=215.0, PDIS = 416.0, 
GAPWIDTH = 220.0, PHI0=phi04;

rf4: RFCavity, VOLT=volt1st, FMAPFN="Cav1.dat", TYPE="SINGLEGAP", 
FREQ=frequency, RMIN = 1900.0, RMAX = 4500.0, ANGLE=305.0, PDIS = 416.0, 
GAPWIDTH = 220.0, PHI0=phi05; 

l1:   Line = (ring,rf0,rf1,rf2,rf3,rf4);

Dist1:DISTRIBUTION, DISTRIBUTION=gauss,
sigmax=  2.0e-03, sigmapx=1.0e-7, corrx=0.0,
sigmay=  2.0e-03, sigmapy=1.0e-7, corry=0.0,
sigmat=  2.0e-03, sigmapt=3.394e-4, corrt=0.0;

Fs1:FIELDSOLVER, FSTYPE=FFT, MX=32, MY=32, MT=32, 
		 PARFFTX=true, PARFFTY=true, PARFFTT=true,
		 BCFFTX=open, BCFFTY=open, BCFFTT=open, BBOXINCR=2;

beam1: BEAM, PARTICLE=PROTON, pc=P0, SPACECHARGE=true, NPART=1e5, BCURRENT=1.0E-3, CHARGE=1.0, BFREQ= frequency;

Select, Line=l1;

track, line=l1, beam=beam1, MAXSTEPS=2000, DT=0.1e-9;
 run, method = "CYCLOTRON-T", beam=beam1, fieldsolver=Fs1, distribution=Dist1;
endtrack;

Stop;
